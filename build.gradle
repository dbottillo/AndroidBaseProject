// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        apply from: 'dependencies-versions.gradle'
        apply from: 'dependencies-all.gradle'

        classpath "com.android.tools.build:gradle:$versions.androidGradlePlugin"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions.gradleVersionsPlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$versions.spotlessPlugin"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.9.1"
}

allprojects {
    repositories {
        google()
        jcenter()
        
    }

    dependencies { apply from: "$rootProject.rootDir/dependencies-groups.gradle" }
}

apply plugin: 'com.diffplug.gradle.spotless'
spotless {
    kotlin {
        target 'src/**/*.kt'
    }
}


subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'
    detekt {
        reports {
            html {
                enabled = true
                destination = file("$projectDir/build/detekt.html")
            }
        }
    }
}

apply plugin: 'com.github.ben-manes.versions'
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'atlassian'].any { qualifier ->
                selection.candidate.version.contains(qualifier)
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
// Top-level build file where you can add configuration options common to all sub-projects/modules.
ext.engBuild = (project.findProperty('engBuild') ?: 'true').toBoolean()

if (engBuild) {
    println("eng build pattern on!")
}

buildscript {
    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        apply from: 'dependencies-versions.gradle'
        apply from: 'dependencies-all.gradle'

        classpath "com.android.tools.build:gradle:$versions.androidGradlePlugin"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions.gradleVersionsPlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$versions.dagger"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        
    }

    dependencies { apply from: "$rootProject.rootDir/dependencies-groups.gradle" }
}

apply plugin: 'com.github.ben-manes.versions'
dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'atlassian', 'm1'].any { qualifier ->
                selection.candidate.version.toLowerCase().contains(qualifier)
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

apply from: 'githooks.gradle'
